plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
    id 'org.springframework.boot' version '2.4.1' apply false
    id 'io.freefair.lombok' version "5.3.0" apply false
    id 'idea'
}

allprojects {
    group 'io.github.csaba-ilonka'
    version '1.1.0'
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        imports {
            //noinspection UnnecessaryQualifiedReference
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    lombok {
        version = "1.18.16"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    clean.doLast {
        delete file('build')
    }
}

idea {
    project {
        jdkName = JavaVersion.VERSION_15
        vcs = 'Git'
    }
    module {
        excludeDirs += file('.idea')
        excludeDirs += file('gradle')
        excludeDirs += file('logs')
        downloadJavadoc = true
        downloadSources = true
    }
}